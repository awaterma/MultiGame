/*
* Copyright (C) 2008 ECOSUR, Andrew Waterman and Max Pimm
* 
* Licensed under the Academic Free License v. 3.0. 
* http://www.opensource.org/licenses/afl-3.0.php
*/

package mx.ecosur.multigame.impl.model.gente;

import java.awt.Dimension;
import java.util.ArrayList;
import java.util.HashSet;
import java.util.List;
import java.util.Map;
import java.util.Set;
import java.util.SortedSet;

import mx.ecosur.multigame.MessageSender;

import mx.ecosur.multigame.enums.GameState;
import mx.ecosur.multigame.enums.MoveStatus;

import mx.ecosur.multigame.model.Game;
import mx.ecosur.multigame.model.Move;

import mx.ecosur.multigame.impl.Color;

import mx.ecosur.multigame.impl.model.GridCell;
import mx.ecosur.multigame.impl.model.GameGrid;
import mx.ecosur.multigame.impl.model.GridGame;
import mx.ecosur.multigame.impl.model.GridPlayer;

import mx.ecosur.multigame.impl.entity.gente.GenteGame;
import mx.ecosur.multigame.impl.entity.gente.GentePlayer;
import mx.ecosur.multigame.impl.entity.gente.GenteMove;

import mx.ecosur.multigame.impl.util.Direction;

#GENTE

#These rules have been seperated from the main Pente rule set for 
#use in the modified game of Pente, GENTE, invented by 
#Dr. Luis Garcia Barrios at ECOSUR, in San Cristobal de Las Casas,
#Chiapas, Mexico.

#Functions for the modified GridGame of PENTE, "GENTE"

#    Determines if the destination of the requested move
#    is in the center of the grid.
function boolean isCenter (GenteGame game, GenteMove move) {
    Dimension size = game.getSize();
    GridCell destination = (GridCell) move.getDestination ();
    
    int centerWidth = (int) size.getWidth()/2;
    int centerHeight = (int) size.getHeight()/2;
    
    boolean ret = (destination.getRow () == centerHeight && 
        destination.getColumn() == centerWidth);
    if (!ret)
        move.setStatus (MoveStatus.INVALID);
    return ret;
}

#    Determines if the destination of the requested move is
#    empty on the grid.
function boolean isEmpty (GameGrid grid, GenteMove move) {
    GridCell destination = (GridCell) move.getDestination();
    GridCell current = grid.getLocation (destination);
    boolean ret = (current == null);
    if (!ret)
        move.setStatus (MoveStatus.INVALID);
    return ret;
}

function int scorePlayer (GenteMove move) {
    int ret = 0;
    GentePlayer player = (GentePlayer) move.getPlayer();
    if (player.getTrias().size() >= 2)
        ret = 10;
    else if (player.getTesseras().size() >= 3) 
        ret = 5;
    return ret;
}

#    Determines if the game has a winner.
function boolean hasWinner (GenteGame game) {
    return (game.getWinners().size() > 0);
}

#    Gets the winner from a specified game.  For more complicated payoff
#    schemas, this function should return a PriorityQueue of Players 
#    based upon positions on the board.
function Set getWinners (GenteGame game) {   
    GenteGame pente = (GenteGame) game;
    return pente.getWinners();
}

function GridPlayer incrementTurn (GenteMove move) {
    GridPlayer player = move.getPlayer();    
    player.setTurn(false);
    GenteGame game = (GenteGame) player.getGame();

    # Find next player 
    List<GridPlayer> players = game.getPlayers();
    int playerNumber = players.indexOf(player);
    GridPlayer nextPlayer = null;
    if (playerNumber == players.size() - 1) {
        nextPlayer = players.get(0);
    } else {
        nextPlayer = players.get(playerNumber + 1);
    }
    
    nextPlayer.setTurn (true);
    return nextPlayer;
}

#Determines if a Tria has already been scored
function boolean hasTria (GridCell cell1, GridCell cell2, GridCell cell3, GenteMove move) {
    BeadString tria = new BeadString (cell1, cell2, cell3);
    for (BeadString test : move.getTrias()) {
        if (test.equals(tria))
            return true;
    }
    
    return false;
}

#Determines if a Tessera has already been scored
function boolean hasTessera (GridCell cell1, GridCell cell2, GridCell cell3, 
    GridCell cell4, GenteMove move) 
{
    BeadString tessera = new BeadString (cell1, cell2, cell3, cell4);
    for (BeadString test : move.getTesseras()) {
        if (test.equals(tessera))
            return true;
    }
    
    return false;
}

#Rules for the game of PENTE

#Sets the players in the game so that Black has the first turn
#Sets up the empty game grid and sets thate game into the PLAY state
rule "setup"
    agenda-group "initialize"
when
    game : GenteGame (state == GameState.BEGIN)
then
    List<GridPlayer> players = game.getPlayers();
    for (GridPlayer p : players) {
        if (p.getColor() == Color.YELLOW)
            p.setTurn(true);
        else
            p.setTurn(false);
    }
    GameGrid grid = new GameGrid ();
    modify (game) { setGrid (grid), setState (GameState.PLAY), setPlayers (players) }
    Game model = new Game (game); 
    MessageSender messageSender = new MessageSender();
    messageSender.sendPlayerChange(model);
    messageSender.sendStartGame(model);
end

#Yellow must move first, and that move can only be at the center of the board
rule "evaluate-first-move"
    agenda-group "verify"
when
    game  : GenteGame (state == GameState.PLAY, $grid : grid)
    move  : GenteMove (status != MoveStatus.VERIFIED, player.turn == true, player.color == Color.YELLOW)
    eval ($grid.getCells().isEmpty())
    eval (isCenter (game, move))    
then
    modify (move) { setStatus (MoveStatus.VERIFIED) }
end

#Subsequent moves must occur on a non-empty board, on empty locations
rule "evaluate-move"
    agenda-group "verify"
when
    game  : GenteGame (state == GameState.PLAY, $grid : grid)
    move  : GenteMove (status == MoveStatus.UNVERIFIED, player.turn == true)
    eval (!$grid.getCells().isEmpty())
    eval (isEmpty (game.getGrid(), move))   
then
    modify (move) { setStatus (MoveStatus.VERIFIED) }
end   

# Executes a simple move
rule "execute-move"
    agenda-group "move"
when 
    game  : GenteGame (state == GameState.PLAY)
    move  : GenteMove (player.turn == true, status == MoveStatus.VERIFIED)  
then 
    GameGrid modifiedGrid = game.getGrid();   
    modifiedGrid.updateCell ((GridCell) move.getDestination());
    modify (game) { setGrid (modifiedGrid) }
    modify (move) { setStatus (MoveStatus.MOVED), setGame (game) }
    insert (move.getDestination());
    Move model = new Move (move);
    MessageSender messageSender = new MessageSender();
    messageSender.sendMoveComplete(new Game(game), model);
end
 
rule "find-vertical-trias"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED)    
    $cell1 : GridCell ($id1 : id, $column1 : column, $row1 : row, $color : color)
    $cell2 : GridCell ($id2 : id != $id1, column == $column1, $row2 : row, color == $color)
    $cell3 : GridCell (id not in ($id1, $id2), column == $column1, $row3 : row, color == $color)
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination))
    eval ($row2 == $row1 + 1 && $row3 == $row2 + 1)
    eval (!hasTria ($cell1, $cell2, $cell3, $move))   
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tria = new BeadString ($cell1, $cell2, $cell3);
    player.addTria(tria);
    if ($move.getSearchCount() >= 7)
        modify ($move) { addTria (tria), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTria (tria), setPlayer (player), setSearchCount (searchCount) }  
end

rule "find-horizontal-trias"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED)    
    $cell1 : GridCell ($id1 : id, $row1 : row, $column1 : column, $color : color)
    $cell2 : GridCell ($id2 : id != $id1, row == $row1, $column2 : column, color == $color)
    $cell3 : GridCell (id not in ($id1, $id2), row == $row1, $column3 : column, color == $color)
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination))
    eval ($column2 == $column1 + 1 && $column3 == $column2 + 1)
    eval (!hasTria ($cell1, $cell2, $cell3, $move))    
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tria = new BeadString ($cell1, $cell2, $cell3);
    player.addTria(tria);
    if ($move.getSearchCount() >= 7)
        modify ($move) { addTria (tria), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTria (tria), setPlayer (player), setSearchCount (searchCount) }  
end

rule "find-right-diagonal-trias"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED)    
    $cell1 : GridCell ($id1 : id, $column1 : column, $row1 : row, $color : color)
    $cell2 : GridCell ($id2 : id != $id1, $column2 : column, $row2 : row, color == $color)
    $cell3 : GridCell (id not in ($id1, $id2), $column3 : column, $row3 : row, color == $color) 
    eval (!hasTria ($cell1, $cell2, $cell3, $move))
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination))
    eval ($row2 == $row1 + 1 && $row3 == $row2 + 1) 
    eval ($column2 == $column1 + 1 && $column3 == $column2 + 1)    
    eval (!hasTria ($cell1, $cell2, $cell3, $move))
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tria = new BeadString ($cell1, $cell2, $cell3);
    player.addTria(tria);
    if ($move.getSearchCount() >= 7) 
        modify ($move) { addTria (tria), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTria (tria), setPlayer (player), setSearchCount (searchCount) }  
end

rule "find-left-diagonal-trias"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED)    
    $cell1 : GridCell ($id1 : id, $column1 : column, $row1 : row, $color : color)
    $cell2 : GridCell ($id2 : id != $id1, $column2 : column, $row2 : row, color == $color)
    $cell3 : GridCell (id not in ($id1, $id2), $column3 : column, $row3 : row, color == $color) 
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination))
    eval ($row2 == $row1 - 1 && $row3 == $row2 - 1) 
    eval ($column2 == $column1 - 1 && $column3 == $column2 - 1)   
    eval (!hasTria ($cell1, $cell2, $cell3, $move))      
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tria = new BeadString ($cell1, $cell2, $cell3);
    player.addTria(tria);
    if ($move.getSearchCount() >= 7) 
        modify ($move) { addTria (tria), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTria (tria), setPlayer (player), setSearchCount (searchCount) }  
end

rule "find-vertical-tesseras"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED,
        $colors : teamColors)               
    $cell1 : GridCell ($id1 : id, $column1 : column, $row1 : row, $color1 : color)
    $cell2 : GridCell ($id2 : id != $id1, column == $column1, $row2 : row, $color2 : color)
    $cell3 : GridCell ($id3 : id not in ($id1, $id2), column == $column1, $row3 : row, $color3 : color)
    $cell4 : GridCell (id not in ($id1, $id2, $id3), column == $column1, $row4 : row, $color4 : color)
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination) 
        || $cell4.equals($destination))        
    eval ($colors.contains($color1) && $colors.contains($color2) && $colors.contains($color3)
        && $colors.contains($color4))
    eval ($row2 == $row1 + 1 && $row3 == $row2 + 1 && $row4 == $row3 + 1)
    eval (!hasTessera ($cell1, $cell2, $cell3, $cell4, $move))    
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tessera = new BeadString ($cell1, $cell2, $cell3, $cell4);
    player.addTessera(tessera);
    GentePlayer partner = (GentePlayer) player.getPartner();
    partner.addTessera(tessera);
    if ($move.getSearchCount() >= 7)
        modify ($move) { addTessera (tessera), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTessera (tessera), setPlayer (player), setSearchCount(searchCount) }  
end

rule "find-horizontal-tesseras"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED,
        $colors : teamColors)               
    $cell1 : GridCell ($id1 : id, $row1 : row, $column1 : column, $color1 : color)
    $cell2 : GridCell ($id2 : id != $id1, row == $row1, $column2 : column, $color2 : color)
    $cell3 : GridCell ($id3 : id not in ($id1, $id2), row == $row1, $column3 : column, $color3 : color)
    $cell4 : GridCell (id not in ($id1, $id2, $id3), row == $row1, $column4 : column, $color4 : color)
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination) 
        || $cell4.equals($destination)) 
    eval ($colors.contains($color1) && $colors.contains($color2) && $colors.contains($color3)
        && $colors.contains($color4)) 
    eval ($column2 == $column1 + 1 && $column3 == $column2 + 1 && $column4 == $column3 + 1)
    eval (!hasTessera ($cell1, $cell2, $cell3, $cell4, $move))           
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tessera = new BeadString ($cell1, $cell2, $cell3, $cell4);
    player.addTessera(tessera);
    GentePlayer partner = (GentePlayer) player.getPartner();
    partner.addTessera(tessera);    
    if ($move.getSearchCount() >= 7) 
        modify ($move) { addTessera (tessera), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTessera (tessera), setPlayer (player), setSearchCount (searchCount) }  
end

rule "find-right-diagonal-tesseras"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED,
        $colors : teamColors)    
    $cell1 : GridCell ($id1 : id, $column1 : column, $row1 : row, $color1 : color)
    $cell2 : GridCell ($id2 : id != $id1, $column2 : column, $row2 : row, $color2 : color)
    $cell3 : GridCell ($id3 : id not in ($id1, $id2), $column3 : column, $row3 : row, $color3 : color)
    $cell4 : GridCell (id not in ($id1, $id2, $id3), $column4 : column, $row4 : row, $color4 : color)
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination)
        || $cell4.equals($destination))
    eval ($row2 == $row1 + 1 && $row3 == $row2 + 1 && $row4 == $row3 + 1)
    eval ($column2 == $column1 + 1 && $column3 == $column2 + 1 && $column4 == $column3 + 1)  
    eval ($colors.contains($color1) && $colors.contains($color2) && $colors.contains($color3)
        && $colors.contains($color4)) 
    eval (!hasTessera ($cell1, $cell2, $cell3, $cell4, $move))    
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tessera = new BeadString ($cell1, $cell2, $cell3, $cell4);
    player.addTessera(tessera);
    GentePlayer partner = (GentePlayer) player.getPartner();
    partner.addTessera(tessera);    
    if ($move.getSearchCount() >= 7)
        modify ($move) { addTessera (tessera), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount (searchCount) }
    else
        modify ($move) { addTessera (tessera), setPlayer (player), setSearchCount (searchCount) }  
end

rule "find-left-diagonal-tesseras"
    agenda-group "evaluate"
    no-loop true 
when
    $game  : GenteGame (state == GameState.PLAY)
    $move  : GenteMove ($destination : destination, status == MoveStatus.MOVED,
        $colors : teamColors)    
    $cell1 : GridCell ($id1 : id, $column1 : column, $row1 : row,  $color1 : color)
    $cell2 : GridCell ($id2 : id != $id1, $column2 : column, $row2 : row,  $color2 : color)
    $cell3 : GridCell ($id3 : id not in ($id1, $id2), $column3 : column, $row3 : row,  $color3 : color)
    $cell4 : GridCell (id not in ($id1, $id2, $id3), $column4 : column, $row4 : row,  $color4 : color)
    eval ($cell1.equals($destination) || $cell2.equals($destination) || $cell3.equals($destination)
        || $cell4.equals($destination))
    eval ($row2 == $row1 - 1 && $row3 == $row2 - 1 && $row4 == $row3 - 1)
    eval ($column2 == $column1 - 1 && $column3 == $column2 - 1 && $column4 == $column3 - 1)
    eval ($colors.contains($color1) && $colors.contains($color2) && $colors.contains($color3)
        && $colors.contains($color4))                    
    eval (!hasTessera ($cell1, $cell2, $cell3, $cell4, $move))    
then
    int searchCount = $move.getSearchCount() + 1;
    GentePlayer player = (GentePlayer) $move.getPlayer();
    BeadString tessera = new BeadString ($cell1, $cell2, $cell3, $cell4);
    player.addTessera(tessera);
    GentePlayer partner = (GentePlayer) player.getPartner();
    partner.addTessera(tessera);    
    if ($move.getSearchCount() >= 7) 
        modify ($move) { addTessera (tessera), setPlayer (player), setStatus(MoveStatus.EVALUATED),
            setSearchCount(searchCount) }
    else
        modify ($move) { addTessera (tessera), setPlayer (player), setSearchCount(searchCount) }  
end

rule "evaluate-nonscoring-move"
    agenda-group "evaluate"
    no-loop true
    salience -10
when
    game  : GenteGame (state == GameState.PLAY)
    move  : GenteMove (player.turn == true, status == MoveStatus.MOVED)
then
    modify (move) { setStatus (MoveStatus.EVALUATED)}
end 

rule "score"
    agenda-group "evaluate"
    no-loop true
when
    game  : GenteGame (state == GameState.PLAY)
    move  : GenteMove (player.turn == true, status == MoveStatus.EVALUATED)
then
    int score = scorePlayer (move);
    GentePlayer player = (GentePlayer) move.getPlayer ();
    if (score > 0) {
        Set<GentePlayer> winners = new HashSet<GentePlayer>();
        if (score == 5) {
            GentePlayer teammate = player.getPartner();
            teammate.setPoints (score);
            winners.add(teammate);
        }
        player.setPoints (score);
        winners.add(player);
        modify (move) { setPlayer (player) }
        modify (game) { setWinners (winners) }
    }
end

# Finds the winner 
rule "find-the-winner"
    agenda-group "evaluate"
    no-loop true
when
    game  : GenteGame (state == GameState.PLAY)
    move  : GenteMove (player.turn == true, status == MoveStatus.EVALUATED)
    eval (hasWinner(game))
then
    Set<GentePlayer> winners = getWinners (game);
    modify (game) { setState (GameState.END), setWinners (winners) }
    Game model = new Game(game);
    MessageSender messageSender = new MessageSender();
	messageSender.sendEndGame (model);
end

rule "increment-turn"
    agenda-group "evaluate"
    no-loop true
    salience -1
when
    game  : GenteGame (state == GameState.PLAY)
    move  : GenteMove (player.turn == true, status == MoveStatus.EVALUATED)
then
    GridPlayer player = incrementTurn (move);
    Game model = new Game(player.getGame());
    MessageSender messageSender = new MessageSender();
    messageSender.sendPlayerChange(model);
end