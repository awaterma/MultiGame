<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" title="Rejoin unfinished game" creationComplete="init(event)">

	<mx:Metadata>
        [Event(name="gameChosen", type="mx.ecosur.multigame.event.GamePlayerEvent")]
    </mx:Metadata>

	<mx:Script> 
        <![CDATA[
        	import mx.ecosur.multigame.enum.GameState;
        	import mx.formatters.DateFormatter;
        	import mx.ecosur.multigame.enum.GameEvent;
        	import mx.ecosur.multigame.event.GamePlayerEvent;

        	import mx.collections.ArrayCollection;
        	import mx.rpc.events.FaultEvent;
        	import mx.rpc.events.ResultEvent;
            import flash.events.MouseEvent;
            import mx.controls.Alert;
            import mx.utils.StringUtil;
            import mx.ecosur.multigame.entity.Player;
            import mx.ecosur.multigame.entity.GamePlayer;
            import mx.ecosur.multigame.entity.Game;
            import mx.ecosur.multigame.manantiales.entity.ManantialesGame;
            import mx.ecosur.multigame.enum.Color;
            import mx.ecosur.multigame.enum.PenteStrategy;
            
            public var player:Player;
            private var _games:ArrayCollection;
            
            public function init(event:Event):void{
            	gameService.getUnfinishedGames(player);
            }
            
            public function reload(event:Event):void {
            	gameService.getUnfinishedGames(player);
            }
            
            private function resultHandler(event:ResultEvent):void{
            	_games = ArrayCollection(event.result);
            	updateDataGrid();
            }   
                        
            private function faultHandler(event:FaultEvent):void{
            	Alert.show(event.fault.faultString, 
            	   "Error getting unfinished games");
            }
            
            private function updateDataGrid():void{
            	var game:Game;
            	var gamePlayer:GamePlayer;
            	var dataProvider:ArrayCollection = new ArrayCollection();
            	var dataItem:Object;
            	var df:DateFormatter = new DateFormatter();
        		df.formatString = "DD/MM/YYYY H:NN";
            	for (var i:Number = 0; i < _games.length; i++){
            		game = Game(_games.getItemAt(i));
            		dataItem = new Object();
            		dataItem.gameType = game.type;
            		dataItem.created = df.format(game.created);
            		dataItem.players = "";
            		for (var j:Number = 0; j < game.players.length; j++){
            			gamePlayer = GamePlayer(game.players[j]);
            			dataItem.players += gamePlayer.player.name + " (" + gamePlayer.color + "), ";
            		}
            		if(dataItem.players.length > 2){
            			dataItem.players = dataItem.players.substring(0, dataItem.players.length - 2);
            		}
            		dataItem.status = GameState.getDescription(game.state);
            		dataProvider.addItem(dataItem);
            	}
            	gamesDataGrid.dataProvider = dataProvider;
            }
            
            private function joinGame():void{
            	var gamePlayer:GamePlayer;
            	var game:Game = Game(_games.getItemAt(gamesDataGrid.selectedIndex));
            	for (var p:String in game.players){
            		gamePlayer = GamePlayer(game.players[p]);
            		if (gamePlayer.player.id == player.id){
            			dispatchEvent(new GamePlayerEvent("gameChosen", gamePlayer));
            			return;
            		}
            	}
            }         
            
        ]]>
    </mx:Script>
    
    <mx:RemoteObject id="gameService" destination="gameService" result="resultHandler(event)" fault="faultHandler(event)" />

    <mx:VBox width="100%">
    	<mx:DataGrid id="gamesDataGrid" width="100%" rowCount="3" minWidth="700">
    		<mx:columns>
				<mx:DataGridColumn headerText="Started" dataField="created" width="120"/>
				<mx:DataGridColumn headerText="Game type" dataField="gameType" width="80"/>
				<mx:DataGridColumn headerText="Players" dataField="players" wordWrap="true" />
				<mx:DataGridColumn headerText="Status" dataField="status" width="120" wordWrap="true"/>
    		</mx:columns>
		</mx:DataGrid>
		<mx:ControlBar horizontalAlign="right" width="100%">
			<mx:Button id="joinBtn" label="Join" click="joinGame()" />
		</mx:ControlBar>
	</mx:VBox>
    
</mx:Panel>
