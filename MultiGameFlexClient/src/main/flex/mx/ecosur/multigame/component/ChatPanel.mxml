<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" styleName="panel" title="Instant Messaging">

	<mx:Script> 
        <![CDATA[
        
        	import mx.controls.Alert;
        	import mx.controls.Text;
        	
			import mx.messaging.messages.AsyncMessage;
			import mx.messaging.messages.IMessage;
			import mx.messaging.events.MessageFaultEvent;
			
			import mx.ecosur.multigame.entity.GamePlayer;
			import mx.ecosur.multigame.entity.ChatMessage;
        	import mx.ecosur.multigame.enum.GameEvent;
			
            public var currentPlayer:GamePlayer;
			
			private function send():void {
				var message:IMessage = new AsyncMessage();
				var chatMessage:ChatMessage = new ChatMessage();
				chatMessage.sender = currentPlayer;
				chatMessage.body = msg.text;
				chatMessage.dateSent = new Date();
				message.body = chatMessage;
				message.headers.GAME_ID = currentPlayer.game.id;
				message.headers.GAME_EVENT = GameEvent.CHAT;
				producer.send(message);
				msg.text = "";
			}
        	
        	public function addMessage(chatMessage:ChatMessage):void {
				log.text += "(" + chatMessage.dateSent.toLocaleTimeString() + ") " + chatMessage.sender.player.name + ": " + chatMessage.body + "\n";
				callLater(updateScroll);
			}
			
			private function faultHandler(event:MessageFaultEvent):void {
				Alert.show("fault in message " + event.faultString);
			}
			
			private function updateScroll():void{
				log.verticalScrollPosition = log.maxVerticalScrollPosition;	
			}
        	        
        ]]>
    </mx:Script>
    
    <mx:Producer id="producer" destination="multigame-destination" fault="faultHandler(event)"/>

	<mx:TextArea id="log" width="100%" height="100%" backgroundColor="#cccccc" borderThickness="0" editable="false" />
	<mx:ControlBar verticalAlign="top">
		 <mx:TextArea id="msg" width="100%" height="40"/>
		 <mx:Button label="Send" click="send()"/> 
	</mx:ControlBar>
</mx:Panel>
