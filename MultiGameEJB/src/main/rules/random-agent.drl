/*
* Copyright (C) 2008 ECOSUR, Andrew Waterman and Max Pimm
* 
* Licensed under the Academic Free License v. 3.0. 
* http://www.opensource.org/licenses/afl-3.0.php
*/

/**
 * This rule package creates a strategic, "Random" player.  The Random player
 * reads the board, looking for any open moves that fulfill the following
 * requirements:
 *
 *   a.  Can connect to another piece the same color as this player
 *   b.  Or can connect to another piece the same color as this player's partner.
 * A randomizer is used to choose from moves that satisfy the above requirements.
 * If no possible moves can be found, then the Random player will place a token
 * randomly, adjacent to a cell of the opposition's colors.
 *
 * @author awaterma@ecosur.mx
*/

package mx.ecosur.multigame.pente.strategy;

import mx.ecosur.multigame.Color;
import mx.ecosur.multigame.GameState;
import mx.ecosur.multigame.ejb.entity.Cell;
import mx.ecosur.multigame.ejb.entity.GameGrid;
import mx.ecosur.multigame.ejb.entity.pente.PenteGame;
import mx.ecosur.multigame.ejb.entity.pente.PenteMove;
import mx.ecosur.multigame.ejb.entity.pente.StrategyPlayer;

import java.util.HashSet;
import java.util.TreeSet;
import java.util.Random;

#Yellow must move first, and that move can only be at the center of the board
rule "first-move"
when
    game   : PenteGame (state == GameState.PLAY, $grid : grid)
    player : StrategyPlayer (turn == true, color == Color.YELLOW)
    eval ($grid.getCells().isEmpty())
then
    PenteMove centerMove = new PenteMove (player, new Cell (
        game.getRows() / 2, game.getColumns () / 2, player.getColor()));
    modify (player) { setNextMove (centerMove) }
end

rule "random-move"
when
    game   : PenteGame (state == GameState.PLAY, $grid : grid)
    player : StrategyPlayer (turn == true, nextMove == null)
    eval (!$grid.getCells().isEmpty())
then
    TreeSet<PenteMove> possible = player.getAvailableMoves();
    if (possible.size() == 0) 
        possible = player.getAvailableMoves (player.getOppositionColors());
    PenteMove destination = null;
    Random random = new Random ();
    for (PenteMove move : possible) {
        if (random.nextBoolean()) { 
            destination = move; 
            break;
        }
    }
    if (destination == null)
        for (PenteMove move : possible) {
            destination = move;
            break;
         }
    modify (player) { setNextMove (destination) }
end